apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

//noinspection GroovyUnusedAssignment
version = "2.2.0"
//noinspection GroovyUnusedAssignment
group = 'net.sourceforge.getl'

configurations {
	addJars
	deployerJars
}

repositories {
	jcenter()
	
	mavenCentral()
	maven { url 'http://repo.novus.com/releases/' }
}

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.5.1'
	compile 'com.jcraft:jsch:0.1.54'
	compile 'net.sf.supercsv:super-csv:2.4.0'
	compile 'com.h2database:h2:1.4.197'
	compile ('org.apache.poi:poi-ooxml:3.15') {
		exclude group: 'stax'
	}
	compile 'com.typesafe:config:1.3.3'
	compile 'org.apache.poi:ooxml-schemas:1.3'
	compile 'org.apache.hadoop:hadoop-hdfs:2.7.3'
	compile 'org.apache.hadoop:hadoop-common:2.7.3'
	compile 'it.sauronsoftware:ftp4j:1.6'
    compile 'net.lingala.zip4j:zip4j:1.3.2'
	compile group: 'com.force.api', name: 'force-partner-api', version: '42.0.0'

	compile 'com.sun.mail:smtp:1.5.6'
	compile 'com.sun.mail:mailapi:1.5.6'

	configurations.compile.extendsFrom(configurations.addJars)

	deployerJars "org.apache.maven.wagon:wagon-http:2.2"
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile fileTree(dir: 'tests', include: '**/*.jar')
}

tasks.withType(GroovyCompile) {
  groovyOptions.optimizationOptions.indy = false
}

task copyDeps(type: Copy) {
    from configurations.runtime
     into "${project.buildDir}/libs.jar"
}

signing {
	required { gradle.taskGraph.hasTask("uploadArchives") }
	sign configurations.archives
}

jar {
	from configurations.addJars.collect { it.isDirectory() ? it : zipTree(it) }
	from { ['docs/getl.readme.txt', 'docs/getl.license.txt', 'docs/getl.license_rus.txt'] }
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
	classifier = 'javadoc'
	from groovydoc.destinationDir
}

artifacts {
	archives javadocJar, sourcesJar
}

build.dependsOn sourcesJar
build.dependsOn javadocJar

//noinspection GroovyMissingReturnStatement
uploadArchives {
	def login = project.properties."ossrhUsername"
	def password = project.properties."ossrhPassword"

	repositories.mavenDeployer {
		//noinspection
		beforeDeployment { deployment -> signing.signPom(deployment) }

		configuration = configurations.deployerJars

		repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
			authentication(userName: login, password: password)
		}

		snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
			authentication(userName: login, password: password)
		}

		pom.project {
			inceptionYear '2018'
			packaging 'jar'
			name 'getl'
			artifactId 'getl'
			groupId 'net.sourceforge.getl'
			description 'GETL - based package in Groovy, which automates the work of loading and transforming data'
			url 'https://sourceforge.net/projects/getl/'

			licenses {
				license {
					name 'GNU GENERAL PUBLIC LICENSE, Version 3'
					url 'http://www.gnu.org/licenses/gpl.txt'
					distribution 'repo'
				}
			}

			scm {
				url 'svn://svn.code.sf.net/p/getl/source/trunk'
			}

			developers {
				developer {
					id 'owner'
					name 'Aleksey Konstantinov'
					email 'ascrus@easydata.ru'
					timezone 'UTC+3'
				}
			}
		}
	}
}

//noinspection GroovyMissingReturnStatement
//uploadArchives {
//    repositories {
//        mavenDeployer {
//            beforeDeployment { /*MavenDeployment*/ deployment -> signing.signPom(deployment) }
//            configuration = configurations.deployerJars
//            repository(url: mavenLocal().url)
//            pom.groupId = rootProject.group
//            pom.artifactId = project.name
//            pom.version = rootProject.version
//        }
//    }
//}